ext {
    appVersion = "3.0.4"
    androidSupportVersion = '25.1.0'
    playServicesVersion = '7.8.0'
    daggerVersion = '2.0'
    leakCanaryVersion = '1.4-beta2'
    butterknifeVersion = '8.0.1'
    eventbusVersion = '3.0.0'
    javaxAnnotationVersion = '10.0-b28'
    timberVersion = '2.7.1'
    ormliteVersion = '4.48'
    clansFabVersion = '1.6.4'
    scribeJavaVersion = '2.8.1'
    stethoVersion = '1.3.1'
    jodatimeVersion = '2.9.4'
    flickr4javaVersion = '2.16'
    mapboxVersion = '4.2.0'
    mapboxServicesVersion = '1.2.1'
    retrofitVersion = '1.9.0'
    okhttpVersion = '2.3.0'
    simplexmlRetrofitVersion = '1.1'
    simplexmlVersion = '2.7.1'
    licensesdialogVersion = '1.8.1'
    arpiglVersion = '0.3.3'
    lovelyDialogVersion = '1.0.4'
    playServicesVersion = '9.2.0'
    multidexSupportVersion = '1.0.1'
    crashlitycsVersion = '2.6.2'
    materialIntroViewVersion = '1.5.2'
    frescoVersion = '0.12.0'
    sqliteJdbcVersion = '3.8.10.1'
    mockitoVersion = '1.10.19'
    robolectricVersion = '2.4'
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        flatDir {
            dirs 'lib'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        // add -Pfoss=true to gradlew args to build a version with no Crashlytics/Google analytics
        if(!project.hasProperty('foss')) {
            classpath "io.fabric.tools:gradle:1.+"
        }
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"

        // this plugin is provided as a jar as atm there is no relase with android supports
        classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.1-SNAPSHOT"

        // BEGIN Dependencies of license-gradle-plugin
        classpath "org.codehaus.plexus:plexus-utils:2.0.5"
        classpath "com.mycila.xmltool:xmltool:3.3"
        // Using compile instead of groovy, so that it goes into the pom
        classpath('com.mycila.maven-license-plugin:maven-license-plugin:1.10.b1') {
            exclude group: 'org.apache.maven', module: 'maven-plugin-api'
            exclude group: 'org.apache.maven', module: 'maven-project'
        }
        // END Dependencies of license-gradle-plugin
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'lib'
    }
}



apply plugin: 'com.android.application'
if(!project.hasProperty('foss')) {
    apply plugin: 'io.fabric'
}
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    useLibrary 'org.apache.http.legacy'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "io.jawg.osmcontributor"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 24
        versionName appVersion
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a"
        }
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "BASE_OSM_URL", '"https://www.openstreetmap.org/api/0.6/"'
        }

        debug {
            versionNameSuffix ".debug-3"
            buildConfigField "String", "BASE_OSM_URL", '"https://www.openstreetmap.org/api/0.6/"'
            //buildConfigField "String", "BASE_OSM_URL", '"https://master.apis.dev.openstreetmap.org/api/0.6/"'
        }
    }

    productFlavors {
        store {
            applicationId "io.jawg.osmcontributor"
            buildConfigField "String", "APP_NAME", '"OSM Contributor"'
            buildConfigField "String", "CRASHLYTICS_PACKAGE_NAME", '"io.jawg.osmcontributor.store"'
            buildConfigField "int", "DEFAULT_ZOOM", "16"
            buildConfigField "int", "ZOOM_VECTORIAL", "18"
            buildConfigField "int", "ZOOM_MAX_PROVIDER", "19"
            buildConfigField "float", "ZOOM_MARKER_MIN", "12.5f"
            buildConfigField "float", "ZOOM_MAX", "21.99f"
            buildConfigField "double", "CENTER_LAT", "48.858370"
            buildConfigField "double", "CENTER_LNG", "2.294481"
            buildConfigField "String", "MAP_URL", '"http://tile.openstreetmap.org/{z}/{x}/{y}.png"'
            buildConfigField "String", "MAP_STYLE_URL", '"https://gist.githubusercontent.com/anonymous/c1fdbffff5c4e25eb267696e8247dec1/raw/1590b94bfc01433b706d51cf117506a760d14199/57a357f3e4b0dc55a4ea6ffa.json"'
            buildConfigField "String", "MAP_STYLE", '"asset://mapnik.json"'
            buildConfigField "String", "BASE_OSM_URL", osmUrl()
            buildConfigField "String", "BASE_OVERPASS_URL", '"http://overpass-api.de/api/interpreter"'
            buildConfigField "String", "FLICKR_API_KEY", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_key") + '"'
            buildConfigField "String", "FLICKR_API_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_secret") + '"'
            buildConfigField "String", "FLICKR_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token") + '"'
            buildConfigField "String", "FLICKR_TOKEN_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token_secret") + '"'
            buildConfigField "String", "MAPBOX_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("mapbox_token") + '"'
        }

        bus {
            applicationId "io.jawg.osmcontributor.bus"
            versionName "1.1"
            versionCode 2

            buildConfigField "String", "APP_NAME", '"Jungle Bus"'
            buildConfigField "String", "CRASHLYTICS_PACKAGE_NAME", '"io.jawg.osmcontributor.store"'
            buildConfigField "int", "DEFAULT_ZOOM", "16"
            buildConfigField "int", "ZOOM_VECTORIAL", "18"
            buildConfigField "int", "ZOOM_MAX_PROVIDER", "19"
            buildConfigField "float", "ZOOM_MARKER_MIN", "12.5f"
            buildConfigField "float", "ZOOM_MAX", "21.99f"
            buildConfigField "double", "CENTER_LAT", "48.858370"
            buildConfigField "double", "CENTER_LNG", "2.294481"
            buildConfigField "String", "MAP_URL", '"http://tile.openstreetmap.org/{z}/{x}/{y}.png"'
            buildConfigField "String", "MAP_STYLE_URL", '"https://gist.githubusercontent.com/anonymous/c1fdbffff5c4e25eb267696e8247dec1/raw/1590b94bfc01433b706d51cf117506a760d14199/57a357f3e4b0dc55a4ea6ffa.json"'
            buildConfigField "String", "MAP_STYLE", '"asset://mapnik.json"'
            buildConfigField "String", "BASE_OSM_URL", osmUrl()
            buildConfigField "String", "BASE_OVERPASS_URL", '"http://overpass-api.de/api/interpreter"'
            buildConfigField "String", "FLICKR_API_KEY", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_key") + '"'
            buildConfigField "String", "FLICKR_API_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_secret") + '"'
            buildConfigField "String", "FLICKR_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token") + '"'
            buildConfigField "String", "FLICKR_TOKEN_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token_secret") + '"'
            buildConfigField "String", "MAPBOX_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("mapbox_token") + '"'
        }
    }
}

configurations {
    compile.exclude module: 'stax'
    compile.exclude module: 'stax-api'
    compile.exclude module: 'xpp3'
}

dependencies {
    // Android
    compile "com.android.support:multidex:$multidexSupportVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-core-utils:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"

    // Dependency injection
    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    // Required for part of the generated code
    provided "org.glassfish:javax.annotation:$javaxAnnotationVersion"

    // Core
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    compile "com.jakewharton.timber:timber:$timberVersion"
    compile "org.greenrobot:eventbus:$eventbusVersion"
    compile "com.j256.ormlite:ormlite-android:$ormliteVersion"
    compile "joda-time:joda-time:$jodatimeVersion"

    if (project.hasProperty('foss')) {
        compile project(":fosstweaks")
    } else {
        compile "com.google.android.gms:play-services-analytics:$playServicesVersion"
    }
    // network
    compile "com.squareup.retrofit:retrofit:$retrofitVersion"
    compile "com.squareup.okhttp:okhttp:$okhttpVersion"
    compile "com.mobprofs:retrofit-simplexmlconverter:$simplexmlRetrofitVersion"
    compile "org.simpleframework:simple-xml:$simplexmlVersion"

    // UI libraries
    compile "com.android.support:cardview-v7:$androidSupportVersion"
    compile "com.android.support:recyclerview-v7:$androidSupportVersion"
    compile "com.android.support:design:$androidSupportVersion"
    compile "com.github.iammert:MaterialIntroView:$materialIntroViewVersion"
    compile "com.yarolegovich:lovely-dialog:$lovelyDialogVersion"
    compile "com.github.clans:fab:$clansFabVersion"
    compile "com.facebook.fresco:fresco:$frescoVersion"

    // Flickr Helper
    compile "com.flickr4java:flickr4java:$flickr4javaVersion"
    compile "com.github.scribejava:scribejava-apis:$scribeJavaVersion"
    compile 'info.guardianproject.netcipher:netcipher:1.2'

    compile("com.mapbox.mapboxsdk:mapbox-android-sdk:$mapboxVersion@aar") {
        transitive = true
    }
    compile("com.mapbox.mapboxsdk:mapbox-android-services:$mapboxServicesVersion@aar") {
        transitive = true
    }
    compile "de.psdev.licensesdialog:licensesdialog:$licensesdialogVersion"
    compile(name:"arpigl-release-$arpiglVersion", ext:"aar")

    compile "com.facebook.stetho:stetho:$stethoVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'commons-logging'
        exclude group: 'org.apache.httpcomponents'
    }
    testCompile "org.xerial:sqlite-jdbc:$sqliteJdbcVersion"
    testCompile 'commons-io:commons-io:1.3.2'
    compile("com.crashlytics.sdk.android:crashlytics:$crashlitycsVersion@aar") {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.11@aar') {
        transitive = true;
    }
}

// Specify the OSM API url to use when building by adding -PosmUrl=myUrl
// Default url is the developer API at https://master.apis.dev.openstreetmap.org/api/0.6
// The official API url is https://api.openstreetmap.org/api/0.6
def osmUrl() {
    def osmUrl = project.getProperties().get("osmUrl")
    return osmUrl != null ? '"' + osmUrl + '"' : '"https://www.openstreetmap.org/api/0.6"'
}

// Load the properties from the given file
def loadPropertiesFile(String name) {
    def props = new Properties()
    props.load(new FileInputStream(name))
    return props
}

checkstyle {
    configFile project.file('checkstyle.xml')
    showViolations true
}

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    configFile project.file('checkstyle.xml')

    // empty classpath
    classpath = files()
}

preBuild.dependsOn('checkstyle')
