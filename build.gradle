ext {
    appVersion = "3.0.24"
    androidSupportVersion = '27.1.1'
    playServicesVersion = '7.8.0'
    daggerVersion = '2.0'
    butterknifeVersion = '8.0.1'
    eventbusVersion = '3.0.0'
    javaxAnnotationVersion = '10.0-b28'
    timberVersion = '2.7.1'
    ormliteVersion = '4.48'
    clansFabVersion = '1.6.4'
    scribeJavaVersion = '2.8.1'
    stethoVersion = '1.5.0'
    jodatimeVersion = '2.9.4'
    flickr4javaVersion = '2.16'
    mapboxVersion = '4.2.2'
    mapboxServicesVersion = '1.2.1'
    retrofitVersion = '2.3.0'
    okhttpVersion = '3.9.1'
    simplexmlRetrofitVersion = '2.3.0'
    simplexmlVersion = '2.7.1'
    licensesdialogVersion = '1.8.1'
    lovelyDialogVersion = '1.0.4'
    playServicesVersion = '15.0.2'
    multidexSupportVersion = '1.0.3'
    materialIntroViewVersion = '1.5.2'
    frescoVersion = '0.12.0'
    sqliteJdbcVersion = '3.8.10.1'
    mockitoVersion = '1.10.19'
    robolectricVersion = '3.5.1'
    rxAndroidVersion = '1.2.1'
    rxJavaVersion = '1.3.8'
    horizontalLoaderVersion = '1.1.0'
    firebaseJobDispatcherVersion = '0.8.5'
    firebaseCoreVersion = '15.0.2'
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        flatDir {
            dirs 'lib'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.google.gms:google-services:3.3.1'
        classpath "io.fabric.tools:gradle:1.31.2"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'lib'
    }
    google()
}



apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


apply plugin: 'checkstyle'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    useLibrary 'org.apache.http.legacy'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "io.jawg.osmcontributor"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 42
        versionName appVersion
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "BASE_OSM_URL", '"https://www.openstreetmap.org/api/0.6/"'
        }

        debug {
            versionNameSuffix ".debug-3"
            buildConfigField "String", "BASE_OSM_URL", '"https://www.openstreetmap.org/api/0.6/"'
        }
    }

    flavorDimensions "default"

    productFlavors {
        store {
            applicationId "io.mapsquare.osmcontributor"
            buildConfigField "String", "APP_NAME", '"OSM Contributor"'
            buildConfigField "int", "MAX_POIS_ON_MAP", "200"
            buildConfigField "int", "MAP_AREA_FACTOR", "200"
            buildConfigField "int", "DEFAULT_ZOOM", "16"
            buildConfigField "int", "ZOOM_VECTORIAL", "18"
            buildConfigField "int", "ZOOM_MAX_PROVIDER", "19"
            buildConfigField "boolean", "WITH_FILTER", "true"
            buildConfigField "float", "ZOOM_MARKER_MIN", "17f"
            buildConfigField "float", "ZOOM_MAX", "21.99f"
            buildConfigField "String", "MAP_URL", '"http://tile.openstreetmap.org/{z}/{x}/{y}.png"'
            buildConfigField "String", "MAP_STYLE_URL", '"https://gist.githubusercontent.com/anonymous/c1fdbffff5c4e25eb267696e8247dec1/raw/1590b94bfc01433b706d51cf117506a760d14199/57a357f3e4b0dc55a4ea6ffa.json"'
            buildConfigField "String", "MAP_STYLE", '"asset://mapnik.json"'
            buildConfigField "String", "BASE_OVERPASS_URL", '"http://overpass-api.de/api/interpreter/"'
            buildConfigField "String", "FLICKR_API_KEY", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_key") + '"'
            buildConfigField "String", "FLICKR_API_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_secret") + '"'
            buildConfigField "String", "FLICKR_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token") + '"'
            buildConfigField "String", "FLICKR_TOKEN_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token_secret") + '"'
            buildConfigField "String", "MAPBOX_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("mapbox_token") + '"'
            buildConfigField "String", "AUTO_COMMIT_CHANGESET", '""'
        }

        bus {
            applicationId "io.jawg.osmcontributor.bus"

            buildConfigField "String", "APP_NAME", '"Jungle Bus"'
            buildConfigField "int", "MAX_POIS_ON_MAP", "300"
            buildConfigField "int", "MAP_AREA_FACTOR", "200"
            buildConfigField "int", "DEFAULT_ZOOM", "16"
            buildConfigField "int", "ZOOM_VECTORIAL", "18"
            buildConfigField "int", "ZOOM_MAX_PROVIDER", "19"
            buildConfigField "float", "ZOOM_MARKER_MIN", "14f"
            buildConfigField "float", "ZOOM_MAX", "21.99f"
            buildConfigField "boolean", "WITH_FILTER", "false"
            buildConfigField "String", "MAP_URL", '"http://tile.openstreetmap.org/{z}/{x}/{y}.png"'
            buildConfigField "String", "MAP_STYLE_URL", '"https://gist.githubusercontent.com/anonymous/c1fdbffff5c4e25eb267696e8247dec1/raw/1590b94bfc01433b706d51cf117506a760d14199/57a357f3e4b0dc55a4ea6ffa.json"'
            buildConfigField "String", "MAP_STYLE", '"asset://mapnik.json"'
            buildConfigField "String", "BASE_OVERPASS_URL", '"http://overpass-api.de/api/interpreter/"'
            buildConfigField "String", "FLICKR_API_KEY", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_key") + '"'
            buildConfigField "String", "FLICKR_API_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_api_secret") + '"'
            buildConfigField "String", "FLICKR_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token") + '"'
            buildConfigField "String", "FLICKR_TOKEN_SECRET", '"' + loadPropertiesFile("conf.properties").getProperty("flickr_token_secret") + '"'
            buildConfigField "String", "MAPBOX_TOKEN", '"' + loadPropertiesFile("conf.properties").getProperty("mapbox_token") + '"'
            buildConfigField "String", "AUTO_COMMIT_CHANGESET", '"Edited bus stop(s)"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    implementation.exclude module: 'stax'
    implementation.exclude module: 'stax-api'
    implementation.exclude module: 'xpp3'
}

dependencies {
    // Android
    implementation "com.android.support:multidex:$multidexSupportVersion"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:support-core-utils:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:preference-v7:$androidSupportVersion"

    // Dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    // Required for part of the generated code
    compileOnly "org.glassfish:javax.annotation:$javaxAnnotationVersion"

    //RX
    implementation "io.reactivex:rxjava:$rxJavaVersion"
    implementation "io.reactivex:rxandroid:$rxAndroidVersion"

    // Core
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "org.greenrobot:eventbus:$eventbusVersion"
    implementation "com.j256.ormlite:ormlite-android:$ormliteVersion"
    implementation "joda-time:joda-time:$jodatimeVersion"

    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"

    // network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:converter-simplexml:$simplexmlRetrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    implementation "org.simpleframework:simple-xml:$simplexmlVersion"

    // UI libraries
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.github.iammert:MaterialIntroView:$materialIntroViewVersion"
    implementation "com.yarolegovich:lovely-dialog:$lovelyDialogVersion"
    implementation "com.github.clans:fab:$clansFabVersion"
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.github.castorflex.smoothprogressbar:library:$horizontalLoaderVersion"

    // Flickr Helper
    implementation "com.flickr4java:flickr4java:$flickr4javaVersion"
    implementation "com.github.scribejava:scribejava-apis:$scribeJavaVersion"
    implementation 'info.guardianproject.netcipher:netcipher:1.2'

    implementation("com.mapbox.mapboxsdk:mapbox-android-sdk:$mapboxVersion@aar") {
        transitive = true
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation("com.mapbox.mapboxsdk:mapbox-android-services:$mapboxServicesVersion@aar") {
        transitive = true
    }
    implementation "de.psdev.licensesdialog:licensesdialog:$licensesdialogVersion"

    implementation "com.facebook.stetho:stetho:$stethoVersion"

    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.firebase:firebase-jobdispatcher:$firebaseJobDispatcherVersion"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation 'org.easytesting:fest-assert-core:2.0M10'
    testImplementation("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'commons-logging'
        exclude group: 'org.apache.httpcomponents'
    }
    testImplementation "org.xerial:sqlite-jdbc:$sqliteJdbcVersion"
    testImplementation 'commons-io:commons-io:1.3.2'
}

// Load the properties from the given file
def loadPropertiesFile(String name) {
    def props = new Properties()
    props.load(new FileInputStream(name))
    return props
}

checkstyle {
    configFile project.file('checkstyle.xml')
    showViolations true
}

task checkstyle(type: Checkstyle) {
    exclude 'src'
    //include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    configFile project.file('checkstyle.xml')

    // empty classpath
    classpath = files()
}

preBuild.dependsOn('checkstyle')

apply plugin: 'com.google.gms.google-services'